 %TITLE 'CHMEMESS - Erase the message line'
MODULE CHMEMESS (				! Erase the message line
		IDENT = '3-001'			! File: CHMEMESS.BLI Edit: GB3001
		) =
BEGIN
!COPYRIGHT (c) DIGITAL EQUIPMENT CORPORATION 1981, 1988.  ALL RIGHTS RESERVED.
!
!THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED ONLY
!IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE AND WITH THE INCLUSION OF 
!THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR ANY OTHER COPIES THEREOF MAY 
!NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY OTHER PERSON.  NO TITLE
!TO AND OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERRED.
!
!THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE WITHOUT NOTICE AND 
!SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT CORPORATION.
!
!DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF ITS 
!SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
!
!
!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	This module erases the message line.
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Bob Kushlis, CREATION DATE: Unknown
!
! MODIFIED BY:
!
! 1-001	- Original.  DJS 04-Feb-1981.  This module was created by
!	extracting the routine EDT$$ERA_MSGLN  from module CHANGE.BLI.
! 1-002	- Regularize headers.  JBS 27-Feb-1981
! 1-003	- Fix module name.  JBS 02-Mar-1981
! 1-004 - Erase both message lines.  SMB 21-Jun-1982
! 1-005	- Reset LASTMSG.  JBS 05-Jul-1982
! 1-006 - Erase to end of screen instead of two lines separately.  SMB 31-Aug-1982
! 1-007 - Move clearing of message flag.  SMB 06-Oct-1982
! 1-008	- Restore the cursor after clearing the message line.  JBS 09-Oct-1982
! 1-009	- Change the call to erase screen.  JBS 23-Oct-1982
! 1-010	- Don't restore the cursor if it has no current location.  JBS 17-Jan-1983
!
! 3-001 - Add updates from V3 source kit.  GB 27-Apr-1983
!--

%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$ERA_MSGLN : NOVALUE;			! Erase the message line

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine
%SBTTL 'EDT$$ERA_MSGLN  - erase the message line'

GLOBAL ROUTINE EDT$$ERA_MSGLN 			! Erase the message line
    : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine erases the message line if there is currently a message
!	on it.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	MSGFLG
!	TIN_ECHOFLG,
!	TIN_ECHOPOS,
!	MESSAGE_LINE,
!
! IMPLICIT OUTPUTS:
!
!	MSGFLG
!	TIN_ECHOFLG,
!	TIN_ECHOPOS,
!	LASTMSG
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	May erase the message line
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$SC_POSCSIF,			! Put cursor position in format buffer
	EDT$$SC_ERAALL;				! Erase to end of screen

    EXTERNAL
	TIN_ECHOFLG,
	TIN_ECHOPOS,
	MESSAGE_LINE,				! The line number for messages
	MSGFLG,					! Indicates a message has been displayed
	LASTMSG,				! The last message displayed (1 = none)
	CUR_COL,				! Current column
	CS_LNO;				! Current line

    LOCAL
	OLD_CS_LNO;

!+
! Check for message.
!-

    IF (.MSGFLG NEQ 0) OR (.TIN_ECHOFLG)
    THEN
	BEGIN

!+
! Reset the message and echo flags.
!-
	MSGFLG = 0;
	TIN_ECHOPOS = 0;
	TIN_ECHOFLG = 0;
	LASTMSG = 1;
!+
! Erase the message lines.
!-
	OLD_CS_LNO = .CS_LNO;
	CS_LNO = .MESSAGE_LINE;
	EDT$$SC_ERAALL ();
	CS_LNO = .OLD_CS_LNO;
!+
! Restore the cursor.
!-

	IF (.CS_LNO GEQ 0) THEN EDT$$SC_POSCSIF (.CS_LNO, .CUR_COL);

	END;

    END;					! of routine EDT$$ERA_MSGLN

END
ELUDOM
